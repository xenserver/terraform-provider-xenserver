---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "xenserver_vm Resource - xenserver"
subcategory: ""
description: |-
  Provides a virtual machine resource.
---

# xenserver_vm (Resource)

Provides a virtual machine resource.

## Example Usage

```terraform
data "xenserver_sr" "sr" {
  name_label = "Local storage"
}

resource "xenserver_vdi" "vdi1" {
  name_label   = "local-storage-vdi-1"
  sr_uuid      = data.xenserver_sr.sr.data_items[0].uuid
  virtual_size = 100 * 1024 * 1024 * 1024
}
resource "xenserver_vdi" "vdi2" {
  name_label   = "local-storage-vdi-2"
  sr_uuid      = data.xenserver_sr.sr.data_items[0].uuid
  virtual_size = 100 * 1024 * 1024 * 1024
}

data "xenserver_network" "network" {}

# Create a Windows 11 VM that is cloned from the base template
resource "xenserver_vm" "windows_vm" {
  name_label       = "Windows VM"
  template_name    = "Windows 11"
  static_mem_max   = 4 * 1024 * 1024 * 1024
  vcpus            = 4
  cores_per_socket = 2
  cdrom            = "win11-x64_uefi.iso"
  boot_mode        = "uefi_security"
  boot_order       = "cdn"

  hard_drive = [
    {
      vdi_uuid = xenserver_vdi.vdi1.uuid,
      bootable = true,
      mode     = "RW"
    },
    {
      vdi_uuid = xenserver_vdi.vdi2.uuid,
      bootable = false,
      mode     = "RO"
    },
  ]

  network_interface = [
    {
      device       = "0"
      network_uuid = data.xenserver_network.network.data_items[0].uuid,
    },
    {
      device = "1"
      other_config = {
        ethtool-gso = "off"
      }
      mac          = "11:22:33:44:55:66"
      network_uuid = data.xenserver_network.network.data_items[1].uuid,
    },
  ]

  other_config = {
    "tf_created" = "true"
  }
}

# Create a Linux VM that is cloned from the custom template
resource "xenserver_vm" "linux_vm" {
  name_label       = "Linux VM"
  template_name    = "CustomTemplate"
  static_mem_max   = 4 * 1024 * 1024 * 1024
  vcpus            = 4
  check_ip_timeout = 60 * 5

  # Don't need to set up a hard drive if the custom template includes it

  # The network interfaces in the custom template would be removed, so we need to create new one by user-defined.
  network_interface = [
    {
      network_uuid = data.xenserver_network.network.data_items[0].uuid,
      device       = "0"
    },
  ]

  connection {
    type     = "ssh"
    user     = "root"
    password = var.password
    host     = self.default_ip
  }

  provisioner "remote-exec" {
    inline = [
      "cat /etc/os-release",
    ]
  }
}

variable "password" {
  type        = string
  description = "The password for the Linux VM"
  sensitive   = true
}

# Create multiple VMs
locals {
  virtual_machines = {
    "windows-vm" = {
      name_label       = "Windows VM"
      template_name    = "Windows 11"
      static_mem_max   = 8 * 1024 * 1024 * 1024
      vcpus            = 4
      hard_drive = [
        {
          vdi_uuid = xenserver_vdi.vdi1.uuid,
          bootable = true,
          mode     = "RW"
        },
      ]
      network_interface = [
        {
          network_uuid = data.xenserver_network.network.data_items[0].uuid,
          device       = "0"
        },
      ]
    }
    "linux-vm" = {
      name_label       = "Linux VM"
      template_name    = "Debian Bullseye 11"
      static_mem_max   = 4 * 1024 * 1024 * 1024
      vcpus            = 2
      hard_drive = [
        {
          vdi_uuid = xenserver_vdi.vdi2.uuid,
          bootable = true,
          mode     = "RW"
        },
      ]
      network_interface = [
        {
          network_uuid = data.xenserver_network.network.data_items[0].uuid,
          device       = "0"
        },
      ]
    }
  }
}

resource "xenserver_vm" "vm" {
  for_each = local.virtual_machines
  name_label        = each.value.name_label
  template_name     = each.value.template_name 
  static_mem_max    = each.value.static_mem_max
  vcpus             = each.value.vcpus
  hard_drive        = each.value.hard_drive
  network_interface = each.value.network_interface
}

output "vm_out" {
  value = {
    for vm in xenserver_vm.vm : vm.name_label => vm
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `name_label` (String) The name of the virtual machine.
- `network_interface` (Attributes Set) A set of network interface attributes to attach to the virtual machine.<br />Set at least one item in this attribute when use it. (see [below for nested schema](#nestedatt--network_interface))
- `static_mem_max` (Number) Statically-set (absolute) maximum memory (bytes). This value acts as a hard limit of the amount of memory a guest can use at VM start time. New values only take effect on reboot.
- `template_name` (String) The template name of the virtual machine which cloned from.

-> **Note:** `template_name` is not allowed to be updated.
- `vcpus` (Number) The number of VCPUs for the virtual machine.

### Optional

- `boot_mode` (String) The boot mode of the virtual machine, default inherited from the template.<br />This value can be one of [`"bios", "uefi", "uefi_security"`].

-> **Note:** `boot_mode` is not allowed to be updated.
- `boot_order` (String) The boot order of the virtual machine, default inherited from the template.<br />This value is a combination string of [`"c", "d", "n"`]. Find more details in [Setting boot order for domUs](https://wiki.xenproject.org/wiki/Setting_boot_order_for_domUs).
- `cdrom` (String) The VDI name in ISO library to attach to the virtual machine, default inherited from the template.
- `check_ip_timeout` (Number) The duration for checking the IP address of the virtual machine. default is 0 seconds, once the value greater than 0, the provider will check the IP address of the virtual machine in the specified duration.
- `cores_per_socket` (Number) The number of core pre socket for the virtual machine, default inherited from the template.
- `dynamic_mem_max` (Number) Dynamic maximum memory (bytes), default same with `static_mem_max`.
- `dynamic_mem_min` (Number) Dynamic minimum memory (bytes), default same with `static_mem_max`.
- `hard_drive` (Attributes Set) A set of hard drive attributes to attach to the virtual machine, default inherited from the template.<br />Set at least one item in this attribute when use it. (see [below for nested schema](#nestedatt--hard_drive))
- `name_description` (String) The description of the virtual machine, default to be `""`.
- `other_config` (Map of String) The additional configuration of the virtual machine, default to be `{}`.
- `static_mem_min` (Number) Statically-set (absolute) minimum memory (bytes), default same with `static_mem_max`. The least amount of memory this VM can boot with without crashing.

### Read-Only

- `default_ip` (String) The default IP address of the virtual machine.
- `id` (String) The test ID of the virtual machine.
- `uuid` (String) The UUID of the virtual machine.

<a id="nestedatt--network_interface"></a>
### Nested Schema for `network_interface`

Required:

- `device` (String) Order in which VIF backends are created by [XAPI](https://github.com/xapi-project/xen-api), default to be `"0"`.<br />If this value is changed, the VIF will be recreated.
- `network_uuid` (String) Network UUID to attach to VIF.

Optional:

- `mac` (String) MAC address of the VIF, default to be a random MAC address generated by XenServer.

-> **Note:** `mac` is not allowed to be updated.
- `other_config` (Map of String) The additional configuration of the network interface, default to be `{}`.Find more details in [advanced-settings-for-network-interfaces](https://docs.xenserver.com/en-us/xenserver/developer/sdk-guide/xs-api-extensions#advanced-settings-for-network-interfaces).

Read-Only:

- `vif_ref` (String)


<a id="nestedatt--hard_drive"></a>
### Nested Schema for `hard_drive`

Required:

- `vdi_uuid` (String) VDI UUID to attach to VBD.<br />**Note**: Using the same VDI UUID for multiple VBDs is not supported.

Optional:

- `bootable` (Boolean) Set VBD as bootable, default to be `false`.
- `mode` (String) The mode the VBD should be mounted with, default to be `"RW"`.<br />Can be set as `"RO"` or `"RW"`.

Read-Only:

- `vbd_ref` (String)

## Import

Import is supported using the following syntax:

```shell
terraform import xenserver_vm.vm 00000000-0000-0000-0000-000000000000
```
